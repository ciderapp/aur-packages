# Maintainer: Core_UK <dev@coredev.uk>

pkgname=cider2-git
pkgver=r2949.d03d7dd9.
pkgrel=1
pkgdesc="Cider2"
arch=("armv7h" "i686" "x86_64")

_repo="project2"
_author="CiderApp"
_branch="main"
#_branch="remix-sweetener"

url="https://github.com/${_author}/${_repo}"
license=('GPL')
optdepends=('libnotify: Playback notifications')
makedepends=('npm' 'nvm' 'fontconfig' 'yarn')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=(
    "${pkgname%-git}::git+https://github.com/${_author}/${_repo}.git#branch=${_branch}"
    "${pkgname%-git}.desktop"
)
sha256sums=('SKIP'
    '8a9b5584642b5a261a9e65701542068eb5b282cc104b33e8a64484708fe5c663')

_ensure_local_nvm() {
  # lets be sure we are starting clean
  which nvm >/dev/null 2>&1 && nvm deactivate && nvm unload

  export NVM_DIR="${srcdir}/${pkgname}-core-${pkgver}/.nvm"
  # The init script returns 3 if version
  #   specified in ./.nvrc is not (yet) installed in $NVM_DIR
  #   but nvm itself still gets loaded ok
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
}

pkgver() {
    cd "${srcdir}/${pkgname%-git}"

    printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" #"${_branch//-/.}"
}

prepare() {
  _ensure_local_nvm
  cd "${srcdir}/${pkgname%-git}"
  nvm install
}

build() {
    _ensure_local_nvm
    cd "${srcdir}/${pkgname%-git}"
    yarn set version stable
    npx -y check-engine && yarn
    yarn dist
}

package() {
    cd "${srcdir}/${pkgname%-git}"

    # Desktop File
    install -Dm644 "${srcdir}/${pkgname%-git}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-git}.desktop"
    # Install the icon
    install -Dm644 "${srcdir}/${pkgname%-git}/src-electron/icons/icon.png" "${pkgdir}/usr/share/icons/hicolor/256x256/apps/${pkgname%-git}.png"
    # Install the build files
    install -d "$pkgdir/opt/"
    install -d "$pkgdir/usr/bin/"

    cd ${srcdir}/${pkgname%-git}/dist/electron/Packaged/linux-unpacked/
    mkdir "${pkgdir}/opt/${pkgname%-git}"
    cp -r --preserve=mode * "${pkgdir}/opt/${pkgname%-git}"
    ln -sf "/opt/${pkgname%-git}/${pkgname%-git}" "${pkgdir}/usr/bin/${pkgname%-git}"

    # License and Readme
    install -d "$pkgdir/usr/share/licenses" "$pkgdir/usr/share/doc"
    install -Dm644 "${srcdir}/${pkgname%-git}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
